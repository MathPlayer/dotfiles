#!/usr/bin/env zsh
#
# ~/.zshrc
#
# The shebang is needed only to set the filetype automatically, not for executing the file.


#########
# Use zprof for debugging startup performance. Keep comented. Nice to have around.
# zmodload zsh/zprof

#########
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

#########
# Load my functions.
source "${HOME}/.bin/commands.sh"

########
# Cache (home)brew path.
if command_exists brew; then
  brew_dir="$(brew --prefix)"
fi

#########
# Custom paths.

# Build an array of custom paths.
typeset -a custom_paths=(
  # Personal bin
  "${HOME}/.bin"
  # *env
  "${HOME}/.pyenv/bin"
  "${HOME}/.nodenv/bin"
  "${HOME}/.rbenv/bin"
  # Rust
  "${HOME}/.cargo/bin"
  # Default folder for pip apps/binaries.
  "${HOME}/.local/bin"
  # TODO: ruby/gem default folder for apps/binaries.
)

if [ ! -z ${brew_dir} ]; then
  # TODO: Check if /usr/local/bin is already in $PATH.
  # If brew is installed in the default path, this adds a duplicate.
  custom_paths+=(
    "${brew_dir}/bin"
    "${brew_dir}/sbin"
    "${brew_dir}/opt/python/libexec/bin"
    "${brew_dir}/opt/curl/bin"
    "${brew_dir}/opt/sqlite/bin"
    "${brew_dir}/opt/llvm/bin"
  )
fi

# Prepend the custom paths to PATH.
prepend_to_path=""
for custom_path in ${custom_paths[@]}; do
  if [ -d "${custom_path}" ]; then
    prepend_to_path="${prepend_to_path}:${custom_path}"
  fi
done
if [ ! -z "${prepend_to_path}" ]; then
  export PATH="${prepend_to_path#:}:${PATH}"
fi
unset prepend_to_path
unset custom_paths


#########
# zgenom

# The SSH plugin requires this setting before loading
zstyle :omz:plugins:ssh-agent agent-forwarding on

# load zgenom
source "${HOME}/.zgenom/zgenom.zsh"

# Check for plugin and zgenom updates every 7 days. This does not increase the startup time.
zgenom autoupdate --background

# create the zgenom init script if it doesn't exist
if ! zgenom saved; then
  echo "Creating a zgenom save"

  # zgenom load zsh-users/zsh-syntax-highlighting
  zgenom load zdharma-continuum/fast-syntax-highlighting

  # Enhanced filetype highlighting
  zgenom load trapd00r/zsh-syntax-highlighting-filetypes

  zgenom load zsh-users/zsh-completions
  zgenom load zsh-users/zsh-autosuggestions

  # Provide susbtring search in history.
  # ZSH only matches the first word in commands using up-line-or-search/down-line-or-search.
  zgenom load zsh-users/zsh-history-substring-search

  zgenom load romkatv/powerlevel10k powerlevel10k

  # enhance *env tools with "eval" commands down in the file
  zgenom load mroth/evalcache

  # Load custom *env files when entering/exiting a folder
  zgenom load Tarrasch/zsh-autoenv

  # Recommend aliases when using full-blown commands
  zgenom load MichaelAquilina/zsh-you-should-use

  # Autocomplete random commands having help messages.
  # zgenom load RobSis/zsh-completion-generator

  #zgenom load marlonrichert/zsh-autocomplete

  # Handle ssh-agent starting
  # TODO: use 'zgenom ohmyzsh' to load the base?
  zgenom ohmyzsh plugins/ssh-agent

  # TODO: find more (useful) plugins
  # - dircolors
  # - integration with KeePassXC

  # save all to init script
  zgenom save

  # Compile local zsh files.
  zgenom compile "$HOME/.zshrc"
fi


#########
# Powerlevel10k theme
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
source ~/.p10k.zsh


#########
# Tools

# Pyenv
export PYENV_ROOT="$(pyenv root)"
eval "$(pyenv init --path)"
_evalcache pyenv init --no-rehash -
(pyenv rehash &) 2> /dev/null
# avoid double virtualenv name mention in prompt, see pyenv-virtualenv info message below:
# pyenv-virtualenv: prompt changing will be removed from future release.
# configure `export PYENV_VIRTUALENV_DISABLE_PROMPT=1' to simulate the behavior.
export PYENV_VIRTUALENV_DISABLE_PROMPT=1

# Nodenv
_evalcache nodenv init --no-rehash -
(nodenv rehash &) 2> /dev/null

# Rbenv
_evalcache rbenv init - --no-rehash
(rbenv rehash &) 2> /dev/null
# TODO: consider trying the compile part to make rbenv faster.
# Last code snippet in first step from: https://github.com/rbenv/rbenv#basic-github-checkout
# $ cd ~/.rbenv && src/configure && make -C src

# sdkman
# TODO: check if this must be at the end of the file
#THIS MUST BE AT THE END OF THE FILE FOR SDKMAN TO WORK!!!
export SDKMAN_DIR="$HOME/.sdkman"
[[ -s "${SDKMAN_DIR}/bin/sdkman-init.sh" ]] && source "${SDKMAN_DIR}/bin/sdkman-init.sh"

# AWS CLI completion
if command_exists aws_completer; then
  autoload -Uz bashcompinit && bashcompinit
  complete -C aws_completer aws
fi


#########
# Shell config

# Change directories without "cd".
setopt AUTO_CD
# Share history between zsh instances.
setopt share_history
# Partial prefix match when pressing up/down keys.
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
# Use ALT/Option + left/right arrows to jump between words.
bindkey "\e\e[D" backward-word
bindkey "\e\e[C" forward-word


#########
# Misc

# Set language defaults.
export LC_ALL="en_US.UTF-8"
export LANG="en_US.UTF-8"
export LC_CTYPE="en_US.UTF-8"

# Set default settings for LESS.
export LESS="-RFX"

# Set text editors.
command_exists vim && export EDITOR="vim"
command_exists nvim && export EDITOR="nvim"
if [ ! -z "${EDITOR}" ]; then
  export VISUAL="${EDITOR}"
fi

# Set make flags with a default cpu count.
if command_exists nproc; then
  export MAKEFLAGS="-j $(nproc)"
elif command_exists sysctl; then
  export MAKEFLAGS="-j $(sysctl -n hw.logicalcpu)"
fi

# Set umask.
umask 027

# Set some sensible ulimits.
ulimit -n 2048


#########
# Aliases.

# list
if command_exists exa; then
  alias ls="exa --group-directories-first"
  alias l="ls"
  alias ll="ls -l"
  alias la="ls -a"
  alias lla="ls -la"
fi

# git
if command_exists git; then
  alias g="git"
  # Convert any git alias from .gitconfig (`git <alias>` gets aliased into shell as `g<alias>`).
  #
  # Idea for improvement: extract the alias itself below.
  # Current: `which gst` shows `gst: aliased to git st`.
  # Desired: `which gst` shows `gst: aliased to git status`.
  for alias in ${${${(0)"$(git config -z --get-regexp '^alias.')"}%%$'\n'*}#alias.}; do
    alias g$alias="git $alias"
  done
fi

# Docker
if command_exists docker; then
  alias d="docker"
  alias dco="docker compose"
  alias dcup="docker compose up"
  alias dcupd="docker compose up -d"
  alias dcdn="docker compose down"
  alias dcps="docker compose ps"
  alias dcpull="docker compose pull"
  alias dcl="docker compose logs"
  alias dclf="docker compose logs -f"
  alias dcstat="docker ps -q | xargs docker stats --no-stream"
fi

# GNU tools on macOS.
# TODO: conditionally load them (e.g. if on macOS).
# TODO: rm -> grm conflicts with git rm
# TODO: get all tools from brew starting with 'g' / in coreutils package or similar.
# TODO: offload loading in sync -> slow shell start.
_gnu_tools=(
  # "basename"
  # "base64"
  # "cat"
  # "chmod"
  # "chown"
  # "cut"
  "dircolors"
  # "dirname"
  "find"
  "head"
  # "md5sum"
  # "nohup"
  # "rm"
  # "rmdir"
  # "seq"
  "sed"
  "tail"
  # "tee"
  "tar"
  # "wc"
  # "who"
  # "whoami"
  # "yes"
)
for _tool in ${_gnu_tools[@]}; do
  command_exists "g${_tool}" && alias ${_tool}="g${_tool}"
done

# Enhance generic tools.
alias df='df -h'
alias du='du -h'
alias less='less -F'
# Add automatic color output for some tools.
for _tool in "grep" "fgrep" "egrep" "diff"; do
  command_exists ${_tool} --color=auto && alias ${_tool}="${_tool} --color=auto"
done

# Load dircolors if existing. XXX: Find a better way to do this.
if command_exists dircolors && [ -r "${HOME}/.dircolors" ]; then
  which dircolors
  eval "$(dircolors -b "${HOME}/.dircolors")" || eval "$(dircolors -b)"
fi

# misc
alias path="echo \${PATH} | tr ':' '\n'"
alias zsh_fpath="echo \${fpath} | tr ' ' '\n'"
alias ftail='tail -f -n+1'
alias myps='ps axo user,tty,ppid,pid,command | grep -vE "grep|ps axo" | grep -E "(USER\s*PPID)|${USER}"'
alias mypsa='ps aux | grep -vE "grep|ps aux" | grep -E "(USER\s*PID)|${USER}"'
alias logssh='ssh > >(tee '${HOME}'/ssh-$(date +"%Y-%m-%d-%H-%M").out) 2> >(tee ssh-$(date +"%Y-%m-%d-%H-%M").err >&2)'


#########
# Work-related configuration.

_work_dir="${HOME}/work"
if [ -d "${_work_dir}" ]; then
  export MY_WORK_DIR="${_work_dir}"
  source_if_exists "${_work_dir}/.zshrc"
fi
unset _work_dir

########
# Cleanup
unset brew_dir

#########
# Use zprof for debugging startup performance. Keep comented. Nice to have around.
# zprof
